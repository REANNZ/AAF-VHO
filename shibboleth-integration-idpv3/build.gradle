apply plugin: 'java'

def vhProperties = new Properties()
vhProperties.load(file('../virtualhome/application.properties').newInputStream())

version = vhProperties.get('app.version')

base {
  archivesName = 'vh-shib-integration'
}

sourceSets {
  main {
    java {
      srcDirs = ['src']
    }
  }
}

compileJava   {
  sourceCompatibility = '17'
  targetCompatibility = '17'

  options.compilerArgs << '-Xlint:deprecation'
}

repositories {
  mavenCentral()
  maven {
    url 'https://build.shibboleth.net/nexus/content/repositories/releases/'
  }
}

dependencies {
  // javax.servlet < 3.0 doesn't have ServletRequest.getServletContext()
  compileOnly 'org.apache.tomcat:tomcat-servlet-api:10.1.6'

  [
    'net.shibboleth.idp:idp-authn-api:5.0.0',
    'net.shibboleth.idp:idp-consent-api:5.0.0',
    'net.shibboleth.idp:idp-profile-api:5.0.0',
    'net.shibboleth.idp:idp-ui:5.0.0',
    'net.shibboleth:shib-support:9.0.0',
    'org.opensaml:opensaml-messaging-api:5.0.0',
    'org.opensaml:opensaml-profile-api:5.0.0',
    'org.opensaml:opensaml-storage-api:5.0.0',
  ].each {
    compileOnly(it) {
      transitive = false
    }
  }
  compileOnly 'com.google.guava:guava:32.1.2-jre'

  compileOnly 'commons-codec:commons-codec:1.16.0'
  compileOnly 'org.apache.commons:commons-lang3:3.13.0'
  compileOnly 'org.apache.httpcomponents.core5:httpcore5:5.2.2'
  compileOnly 'org.apache.httpcomponents.client5:httpclient5:5.2.2'
  compileOnly 'org.springframework:spring-core:6.0.11'
  compileOnly 'com.googlecode.json-simple:json-simple:1.1.1'
  compileOnly 'org.slf4j:slf4j-api:2.0.7'
}
