apply plugin: 'java'

def vhProperties = new Properties()
vhProperties.load(file('../virtualhome/application.properties').newInputStream())

version = vhProperties.get('app.version')
archivesBaseName = 'vh-shib-integration'

sourceSets {
  main {
    java {
      srcDirs = ['src']
    }
  }
}

compileJava   {
  sourceCompatibility = '11'
  targetCompatibility = '11'
}

repositories {
  mavenCentral()
  maven {
    url 'https://build.shibboleth.net/nexus/content/repositories/releases/'
  }
}

dependencies {
  // javax.servlet < 3.0 doesn't have ServletRequest.getServletContext()
  compile 'org.apache.tomcat:tomcat-servlet-api:8.5.39'

  [
    'net.shibboleth.idp:idp-authn-api:4.0.1',
    'net.shibboleth.idp:idp-ui:4.0.1',
    'net.shibboleth.idp:idp-consent-api:4.0.1',
    'net.shibboleth.utilities:java-support:8.0.0',
    'org.opensaml:opensaml-profile-api:4.0.1',
    'org.opensaml:opensaml-messaging-api:4.0.1',
  ].each {
    compile(it) {
      transitive = false
    }
  }
  compile 'com.google.guava:guava:28.2-jre'

  compile 'commons-codec:commons-codec:1.14'
  compile 'commons-lang:commons-lang:2.6'
  compile 'org.apache.httpcomponents:httpcore:4.4.13'
  compile 'org.apache.httpcomponents:httpclient:4.5.12'
  compile 'com.googlecode.json-simple:json-simple:1.1.1'
  compile 'org.slf4j:slf4j-api:1.7.30'
}
